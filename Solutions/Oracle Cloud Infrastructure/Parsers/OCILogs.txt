// Usage Instruction :
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias as OCILogs.
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. OCILogs | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
OCI_Logs_CL
| extend EventVendor = 'Oracle'
| extend EventProduct = 'Oracle Cloud Infrastructure'
| extend EventStartTime = coalesce(unixtime_seconds_todatetime(column_ifexists("data_startTime_d", long(null))), time_t)
| extend EventEndTime = coalesce(unixtime_seconds_todatetime(column_ifexists("data_endTime_d", long(null))), time_t)
| extend SrcIpAddr=coalesce(column_ifexists("data_sourceAddress_s",''),data_identity_ipAddress_s)
, SrcPortNumber=column_ifexists("data_sourcePort_d", '')
, DstIpAddr=column_ifexists("data_destinationAddress_s",'')
, DstPortNumber=column_ifexists("data_destinationPort_d",'')
, DstBytes=column_ifexists("data_bytesOut_d",'')
, NetworkProtocol=column_ifexists("data_protocolName_s",'')
, data_stateChange_current_Instance_displayName_s = column_ifexists("data_stateChange_current_Instance_displayName_s",'')
, data_stateChange_current_userName_s = column_ifexists("data_stateChange_current_userName_s",'')
, data_request_headers_oci_original_url_s = column_ifexists("data_request_headers_oci_original_url_s", '')
, data_action_s = column_ifexists("data_action_s",'')
| project-rename EventType=type_s
| project-rename EventMessage=data_message_s
| project-rename HttpUserAgentOriginal=data_identity_userAgent_s
| project-rename HttpStatusCode=data_response_status_s
| project-rename HttpRequestMethod=data_request_action_s


